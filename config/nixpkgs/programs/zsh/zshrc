################################################################################
# .zshrc
# Charles Cruz
################################################################################

################################################################################
# Powerlevel10k
################################################################################
source ~/.nix-profile/share/zsh-powerlevel10k/powerlevel10k.zsh-theme
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
[[ ! -f ~/.config/zsh/p10k.zsh ]] || source ~/.config/zsh/p10k.zsh

################################################################################
# General config
################################################################################
export HISTSIZE=100000      # History file size
setopt HIST_IGNORE_DUPS     # Don't record an entry that was just recorded again
setopt HIST_IGNORE_ALL_DUPS # Delete old recorded entry if new one is the same
setopt HIST_SAVE_NO_DUPS    # Don't write duplicate entries in the history file
setopt HIST_IGNORE_SPACE    # Ignore entries that start with a space
setopt SH_WORD_SPLIT        # Bash-like variable expansion
setopt rmstarsilent         # Prevent zsh prompt when doing rm -f
setopt extendedglob         # Use extended glob patterns (for external config)
unsetopt correct_all        # Stop ZSH corrections (because it's not smart)

mkdir -p $HOME/.cache       # Ensure cache directory
export LESSHISTFILE=$HOME/.cache/lesshst                # less history file
export NODE_REPL_HISTORY=$HOME/.cache/node_repl_history # nodejs history file

alias cat="bat -p"
alias diff="batdiff"
alias doom="~/.emacs.d/bin/doom"
alias g="git"
alias grep="grep --color=always"
alias ll="TZ=UTC exa -aghl --group-directories-first"
alias ls="TZ=UTC exa"
alias lt="TZ=UTC exa --long --tree"
alias man="batman"
alias mkex="chmod u+x"
alias nodejs="node"
alias t="tmux"
alias untar="tar -xzf"
alias vim="nvim"
alias watch="batwatch"

add-to-path () {
  while [ -n "$1" ]; do
    case ":$PATH:" in
      *":$1:"*) :;;
      *) PATH="$1:$PATH";;
    esac
    shift
  done
  export PATH
}

bb () {
  if [ "$#" -eq 0 ]; then
    command rlwrap bb
  else
    command bb "$@"
  fi
}

emacs () {
 TERM=xterm-24bits command emacs -nw "$@"
}

env-vars () {
  printenv | sort | bat --pager cat -pl ini
}

hash-check () {
  if [ "$#" -ne 3 ]; then
    >&2 echo "Expected 3 args (hash-check <type> <file> <hash>)"; return
  fi
  local type=$1
  local file=$2
  local this_hash=$3
  local that_hash
  case "$type" in
    md5)    type=md5sum ;;
    sha1)   type=sha1sum ;;
    sha256) type=sha256sum ;;
    *)      >&2 echo "Unexpected type (expected: md5, sha1, sha256)"; return ;;
  esac
  that_hash=$($type $file | cut -f 1 -d ' ')
  if [ "$this_hash" == "$that_hash" ]; then
    echo "Match!"
  else
    echo "Mismatch! \"$this_hash\" != \"$that_hash\""
  fi
}

npm () {
  case $1 in
    packages) shift; command npm list --depth 0 "$@" ;;
    *)        command npm "$@" ;;
  esac
}

nx () {
  local cmd=$1
  case $cmd in
    install)   shift; command nix-env -iA "$@" ;;
    uninstall) shift; command nix-env --uninstall "$@" ;;
    search)    shift; command nix-env -qa --json "$@" ;;
    list)      shift; command nix-env --query ;;
    *)         command nix-env "$@" ;;
  esac
}

paths () {
  echo $PATH | sed -e s/\\:/\\\n/g
}

################################################################################
# Os-specific config
################################################################################
case "$(uname -s)" in
  Linux*)
    case "$(cat /etc/os-release | sed -En 's/^NAME="(.+)"$/\1/p')" in
      Ubuntu)
        add-to-path $HOME/bin $HOME/.local/share/npm/bin
        alias install="sudo apt install"
        alias remove="sudo apt remove"
        alias upgrade="sudo apt update && sudo apt -y upgrade"
        alias package="apt list -qq $@"
        ;;
      *) ;;
    esac
    ;;

  Darwin*)
    # Homebrew
    add-to-path /usr/local/bin /usr/local/sbin
    alias brewski="brew update && brew upgrade"
    ;;
esac

################################################################################
# Nix
################################################################################
if [ -e $HOME/.nix-profile/etc/profile.d/nix.sh ]; then
  source $HOME/.nix-profile/etc/profile.d/nix.sh
fi
export NIX_PAGER=cat

################################################################################
# External config (includes files in the format `.zshrc-*`, but excludes those
# with an additional extension like `.zshrc-.bak` via path modifiers)
################################################################################
for f in $HOME/.zshrc-*; do [ -z $f:t:r ] && source $f; done
